buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}
apply plugin: 'com.jfrog.bintray'
apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.6
targetCompatibility = 1.6

// This is in case we ever use non-ASCII chars in sources
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    implementation 'xmlpull:xmlpull:1.1.3.1'
    testImplementation 'junit:junit:4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

artifacts {
    archives sourcesJar, javadocJar
}

allprojects {
    tasks.withType(JavaCompile) {
        // Warn about deprecations
        options.compilerArgs << '-Xlint:deprecation'
        // Warn about unchecked usages
        options.compilerArgs << '-Xlint:unchecked'

        options.debug(['debugLevel': 'source,lines,vars'])
        options.debug = true
    }

    tasks.withType(AbstractArchiveTask) {
        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }
}

bintray {
    user = rootProject.hasProperty('BINTRAY_USER') ? BINTRAY_USER : ''
    key = rootProject.hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : ''
    configurations = ['archives']

    publish = true
    override = true

    pkg {
        repo = 'maven'
        name = "${GROUP}:${POM_NAME}"
        desc = POM_DESCRIPTION
        licenses = [POM_LICENCE_ID]
        labels = ['xmlparser', 'xml', 'java']
        websiteUrl = POM_URL
        issueTrackerUrl = POM_SCM_ISSUES_URL
        vcsUrl = POM_SCM_URL
        githubRepo = POM_SCM_URL

        version {
            name = VERSION_NAME
            released  = new Date()
            vcsTag = VERSION_NAME
            gpg {
                sign = true
            }
        }
    }
}
